#@TYPE: Machine
#@NAME: kv260
#@DESCRIPTION: Machine support for Kria KV260 Vision AI Starter Kit customized for LmP

DEFAULTTUNE = "cortexa53"
SOC_VARIANT = "ev"

# Set MACHINEOVERRIDES manually instead of using BOARD / BOARD_VARIANT (now deprecated)
MACHINEOVERRIDES =. "k26:k26-kv:"

# Carriers are configured via overlays
UBOOT_DTBO_LOADADDRESS ?= "0x40100000"

# Build boot.scr for compatibility with the stock firmware
WKS_FILE_DEPENDS:append = " u-boot-ostree-scr"
IMAGE_BOOT_FILES:append = " boot.scr uEnv.txt"
# meta-kria drops u-boot-xlnx-uenv
EXTRA_IMAGEDEPENDS:remove = "u-boot-xlnx-uenv"
WKS_FILE_DEPENDS:remove = "u-boot-xlnx-uenv"
MACHINE_ESSENTIAL_EXTRA_RDEPENDS:remove = "u-boot-xlnx-uenv"

require conf/machine/include/zynqmp-lmp.inc

# require conf/machine/include/k26-smk.inc
HDF_MACHINE = "k26-kv"

# DSA file taken from xilinx-k26-starterkit-v2021.1-final.bsp
HDF_BASE = "file://"
HDF_PATH = "system.xsa"
HDF_EXT = "xsa"

YAML_SERIAL_CONSOLE_BAUDRATE = "115200"

# bitstream support
MACHINE_FIRMWARE:append = " fpga-manager-script"
MACHINE_EXTRA_RRECOMMENDS += "${MACHINE_FIRMWARE}"
SPL_FPGA_BINARY = "bitstream.bit.bin"
SPL_FPGA_LOAD_ADDR = "0x18000000"

# Make image-update essential as it is required for bootfw updates
MACHINE_ESSENTIAL_EXTRA_RDEPENDS += "image-update"

# SOM has infineon slb9670
MACHINE_FEATURES += "tpm2"
